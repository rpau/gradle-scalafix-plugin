// add the build script
buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'net.researchgate:gradle-release:2.6.0'
        classpath "com.adtran:scala-multiversion-plugin:1.+"
    }
}

plugins {
    id 'java'
    id 'java-gradle-plugin'
}

apply plugin: 'scala'
apply plugin: 'maven'
apply plugin: 'maven-publish'
//apply plugin: 'signing'
//declare the release plugin
apply plugin: 'net.researchgate.release'
apply plugin: "com.adtran.scala-multiversion-plugin"

configurations {
    scalaCompilerPlugin
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile "org.scala-lang:scala-library:$scalaVersion"
    compile "ch.epfl.scala:scalafix-cli_$scalaVersion:0.9.4"
    testCompile group: 'junit', name: 'junit', version: '4.12'
    scalaCompilerPlugin "org.scalameta:semanticdb-scalac_$scalaVersion:4.1.4"
}


compileScala {
    scalaCompileOptions.additionalParameters = ['-Yrangepos'] // required by semanticdb-scalac
}

tasks.withType(ScalaCompile) {
    scalaCompileOptions.additionalParameters = [
            "-Xplugin:" + configurations.scalaCompilerPlugin.asPath,
            "-Ywarn-unused"
    ]
}

// Create a jar with javadoc
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

// Create a jar with soruces
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// hook all the artifacts under artifacts task
artifacts {
    archives javadocJar, sourcesJar
}

/*
signing {
    sign configurations.archives
}*/

def v_ossrhUsername="FOO"
def v_ossrhPassword="BAR"

if (project.hasProperty("ossrhUsername")) {
    v_ossrhUsername = ossrhUsername
}
if (project.hasProperty("ossrhPassword")) {
    v_ossrhPassword = ossrhPassword
}
//https://docs.gradle.org/current/userguide/publishing_maven.html

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: v_ossrhUsername, password: v_ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: v_ossrhUsername, password: v_ossrhPassword)
            }

            pom.project {
                name 'gradle-scalafix-plugin'
                packaging 'jar'
                artifactId archivesBaseName
                version version
                description 'A gradle plugin for scalafix'
                url 'https://github.com/rpau/gradle-scalafix-plugin'

                scm {
                    connection 'scm:git:git://github.com/rpau/gradle-scalafix-plugin.git'
                    developerConnection 'scm:git:git@github.com:rpau/gradle-scalafix-plugin.git'
                    url 'https://github.com/rpau/gradle-scalafix-plugin'
                }

                licenses {
                    license {
                        name 'Apache 2 License'
                        url 'https://www.apache.org/licenses/LICENSE-2.0'
                    }
                }

                developers {
                    developer {
                        id 'rpau'
                        name 'Raquel Pau'
                        email 'rpau@walkmod.com'
                    }
                }
            }
        }
    }
}